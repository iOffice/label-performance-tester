<?xml version="1.0"?>
<s:Application
		xmlns:fx="http://ns.adobe.com/mxml/2009"
		xmlns:s="library://ns.adobe.com/flex/spark"
		width="800" height="800"
		applicationComplete="createLabels();">

	<fx:Script><![CDATA[
		import mx.events.EffectEvent;

		import spark.components.Label;

		import util.Logger;

		public static const TEST_OBJECT_COUNT:int = 10000;

		private var addingStartTime:Date;

		private var labelList:Vector.<Label> = new Vector.<Label>();

		private function createLabels():void {
			var startTime:Date = new Date();
			var i:int;
			var count:int = TEST_OBJECT_COUNT;
			for (i = 0; i < count; i++) {
				var label:Label = new Label();
				label.x = Math.random() * 700;
				label.y = Math.random() * 700;
				label.text = "This is a label.";
				labelList.push(label);
				labelGroup.addElement(label);
			}
			Logger.log(startTime, "creating");
			addingStartTime = new Date();
			callLater(panLabels);
		}

		private function panLabels():void {
			Logger.log(addingStartTime, "adding");
			var startTime:Date = new Date();
			panSequence.play();
			panSequence.addEventListener(EffectEvent.EFFECT_END,
					function (event:EffectEvent):void {
						Logger.log(startTime, "panning");
						scaleLabels();
					});
		}

		private function scaleLabels():void {
			var startTime:Date = new Date();
			scaleSequence.play();
			scaleSequence.addEventListener(EffectEvent.EFFECT_END,
					function (event:EffectEvent):void {
						Logger.log(startTime, "scaling");
						destroyLabels();
					});
		}

		private function destroyLabels():void {
			var startTime:Date = new Date();
			var i:int;
			var count:int = labelList.length;
			for (i = 0; i < count; i++) {
				var label:Label = labelList.shift();
				labelGroup.removeElement(label);
			}
			Logger.log(startTime, "destroying");
		}
		]]></fx:Script>

	<fx:Declarations>
		<s:Sequence id="panSequence" targets="{[labelGroup]}">
			<s:Animate duration="1000">
				<s:SimpleMotionPath property="x" valueFrom="0" valueTo="700"/>
				<s:SimpleMotionPath property="y" valueFrom="0" valueTo="700"/>
			</s:Animate>
			<s:Animate duration="1000">
				<s:SimpleMotionPath property="x" valueFrom="700" valueTo="0"/>
				<s:SimpleMotionPath property="y" valueFrom="700" valueTo="0"/>
			</s:Animate>
		</s:Sequence>

		<s:Sequence id="scaleSequence" targets="{[labelGroup]}">
			<s:Animate duration="1000">
				<s:SimpleMotionPath property="scaleX" valueFrom="1.0" valueTo="0.1"/>
				<s:SimpleMotionPath property="scaleY" valueFrom="1.0" valueTo="0.1"/>
			</s:Animate>
			<s:Animate duration="1000">
				<s:SimpleMotionPath property="scaleX" valueFrom="0.1" valueTo="1.0"/>
				<s:SimpleMotionPath property="scaleY" valueFrom="0.1" valueTo="1.0"/>
			</s:Animate>
		</s:Sequence>
	</fx:Declarations>

	<s:Group id="labelGroup"/>

</s:Application>
