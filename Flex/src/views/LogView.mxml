<?xml version="1.0"?>
<s:Group
		xmlns:fx="http://ns.adobe.com/mxml/2009"
		xmlns:s="library://ns.adobe.com/flex/spark"
		enterFrame="tick();">

	<fx:Script><![CDATA[
		protected static const NEW_LINE:String = "\r\n";

		protected static function formatMilliseconds(value:int):String {
			var seconds:String = String(Math.floor(value / 1000));
			var milliseconds:String = pad(String(value % 1000), "0", 3);
			seconds = seconds + "." + milliseconds;
			return pad(seconds, " ", 0);
			//return pad(seconds, " ", 12);
		}

		protected static function formatTime(time:Date):String {
			var hours:String = pad(String(time.hours), "0", 2);
			var minutes:String = pad(String(time.minutes), "0", 2);
			var seconds:String = pad(String(time.seconds), "0", 2);
			var milliseconds:String = pad(String(time.milliseconds), "0", 3);
			return hours + ":" + minutes + ":" + seconds + "." + milliseconds;
		}

		protected static function pad(string:String, pad:String, length:int):String {
			while (string.length < length) {
				string = pad + string;
			}
			return string;
		}

		protected var fpsLow:uint = 100;
		protected var memHigh:Number = 0.0;
		public var stats:Stats;

		public function logActionTime(startTime:Date, action:String):void {
			var endTime:Date = new Date();
			var diff:int = endTime.getTime() - startTime.getTime();
			if (logLabel.text.length > 0)
				logString("");
			logString(action);
			//logString(formatTime(startTime) + " - started " + action);
			logString("seconds elapsed - " + formatMilliseconds(diff) + " seconds");
			//logString(formatTime(endTime) + " - finished " + action);

			var fpsString:String = "N/A";
			if (fpsLow < 100)
				fpsString = String(fpsLow) + " fps";
			logString("fps peak low - " + fpsString);
			fpsLow = 100;

			var memoryString:String = "N/A";
			if (memHigh > 0.0)
				memoryString = memHigh.toFixed(1) + " MB";
			logString("memory peak high - " + memoryString);
			memHigh = 0.0;
		}

		public function logString(string:String):void {
			if (logLabel.text.length > 0)
				logLabel.text += NEW_LINE;

			logLabel.text += string;
		}

		public function tick():void {
			if (!stats)
				return;

			var fps:uint = stats.fps;
			if (fps < fpsLow)
				fpsLow = fps;

			var mem:Number = stats.mem;
			if (mem > memHigh)
				memHigh = mem;
		}
		]]></fx:Script>

	<s:layout>
		<s:VerticalLayout/>
	</s:layout>

	<s:TextArea id="logLabel" width="100%" height="100%" fontFamily="_typewriter"/>

</s:Group>
